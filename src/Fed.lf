target uC {
    // platform: Native,
    platform: {
        name: Zephyr,
        // board: w5500_evb_pico
        board: nrf54l15dk/nrf54l15/cpuapp
        // board: nrf52840dk/nrf52840
    },
    net-interface: sicslowpan,
    logging: warn,
    clock-sync: on
}

preamble {=

  #include "reactor-uc/reactor-c.h"

=}

reactor Source {
preamble {=
    #include <zephyr/kernel.h>
    #include <zephyr/drivers/gpio.h>
    #include <assert.h>
    #define LED0_NODE DT_PATH(leds, led_0)
    static const struct gpio_dt_spec led = GPIO_DT_SPEC_GET(LED0_NODE, gpios);
=}

    reaction(startup) {=
        assert(device_is_ready(led.port));
        gpio_pin_configure_dt(&led, GPIO_OUTPUT_ACTIVE);
    =}

    output out: int;

    state counter: int = 0;

    timer t(0, 250 msec);
    reaction(t) -> out {=
        self->counter++;

        lf_set(out, self->counter);
        printf("Source sending out value: %d\n", self->counter);
        interval_t ts = lf_time_logical_elapsed();
        interval_t Ts = lf_time_physical_elapsed();

        printf(
            "  SOURCE logical time: %lld ms, physical time: %lld ms, lag: %lld ms\n",
            ts / 1000000, Ts / 1000000, (Ts-ts) / 1000000
        );

        gpio_pin_toggle_dt(&led);
    =}
}

reactor Sink {
preamble {=
    #include <zephyr/kernel.h>
    #include <zephyr/drivers/gpio.h>
    #include <assert.h>
    #define LED0_NODE DT_PATH(leds, led_0)
    static const struct gpio_dt_spec led = GPIO_DT_SPEC_GET(LED0_NODE, gpios);
=}

    reaction(startup) {=
        assert(device_is_ready(led.port));
        gpio_pin_configure_dt(&led, GPIO_OUTPUT_ACTIVE);
    =}

    input in: int;

    reaction(in) {=
        printf("Sink received value; %d\n", in->value);
        interval_t ts = lf_time_logical_elapsed();
        interval_t Ts = lf_time_physical_elapsed();

        FederatedEnvironment* envFed = (FederatedEnvironment*)env;

        interval_t TsF = envFed->clock.to_hw_time(&envFed->clock, Ts);

        printf(
            "  SINK logical time: %lld ms, physical time: %lld ms, lag: %lld ms\n",
            ts / 1000000, Ts / 1000000, (Ts-ts) / 1000000
        );
        printf("  Comparing physical time: Local: %lld ms, ClockSyn: %lld ms\n", Ts / 1000000, TsF / 1000000);

        gpio_pin_toggle_dt(&led);
    =}
}

federated reactor {
  @board("nrf54l15dk/nrf54l15/cpuapp")
  srcFed = new Source();
  @board("nrf52840dk/nrf52840")
  snkFed = new Sink();

  @buffer(5)
  srcFed.out -> snkFed.in;
}